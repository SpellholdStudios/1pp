/* ================================================================================================================================================================================== *
 *  v 4.2.0                                                                                                                                                                           *
 *                                               C O M P O N E N T   :   R e s t o r e d   f l a m e   s w o r d   a n i m a t i o n s                                                *
 *                                                                                                                                                                                    *
 * ================================================================================================================================================================================== *
 *  This component restores separate flame sword animations for Baldur's Gate II (similar to the ones in the original).                                                               *
 *  It also includes flame short swords, adding a new item type to the game. (Original code by Nythrun!)                                                                              *
 * ================================================================================================================================================================================== *
 *  change-log:                                                                                                                                                                       *
 *  -----------                                                                                                                                                                       *
 *     - Coding simplification:                                                                                                                                                       *
 *          Copied entire folders instead of infinite lines (COPY ~1pp/folder/file.ext~ ~override~ or ACTION_FOR_EACH file IN list BEGIN COPY ~1pp/folder/file.ext~ ~override~ END).  *
 *          Used new 1pp_compatibility.tph library to improve 1PP crossmods compatibility (Improved HaerDalis Swords).                                                                *
 * ================================================================================================================================================================================== */

// ----------------------------- //
//     ITEMS               line  //
// ----------------------------- //
//  FLAME LONG SWORDS    #   25  //
//  FLAME SHORT SWORDS   #  136  //
//  MODS FLAME SWORDS    #  258  //
// ----------------------------- //

/* =================== *
 *  FLAME LONG SWORDS  *
 * =================== */

PRINT @203000 // ~Installing flame long swords...~

PRINT @203001 // ~Copying...~
SILENT

COPY ~1pp/additions/obj_anim/fs~ ~override~

PRINT @203002 // ~Patching...~
SILENT

ACTION_FOR_EACH ~item~ IN ~blakblad~ ~bonedag~ ~morsword~ ~rodsword~ ~sw1h98~ ~_sw1h98~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%item%.itm~) BEGIN
		COPY_EXISTING ~%item%.itm~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
				WRITE_ASCII 0x22 ~S1~	// Long sword - replaces FS Flaming sword
			END
		BUT_ONLY
	END
END

// Sword of Flame +1 - Angurvadal +4 - Angurvadal +5
ACTION_FOR_EACH item IN ~sw1h53~ ~sw1h60~ ~sw1h61~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%item%.itm~) BEGIN
		COPY_EXISTING ~%item%.itm~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
				READ_LONG  0x64 "ho"
				READ_SHORT 0x68 "hc"
				READ_LONG  0x6a "eo"
				READ_SHORT 0x70 "ge"
				SET "blade" = (~%SOURCE_RES%~ STRING_EQUAL ~sw1h53~ ? 0x9fefff00 :
							   ~%SOURCE_RES%~ STRING_EQUAL ~sw1h60~ ? 0x9fefff00 :
							   ~%SOURCE_RES%~ STRING_EQUAL ~sw1h61~ ? 0x9fefff00 : 0x00)
				SET "grip"  = (~%SOURCE_RES%~ STRING_EQUAL ~sw1h53~ ? 0x20a5da00 :
							   ~%SOURCE_RES%~ STRING_EQUAL ~sw1h60~ ? 0x20a5da00 :
							   ~%SOURCE_RES%~ STRING_EQUAL ~sw1h61~ ? 0x20a5da00 : 0x00)
				SET "minor" = (~%SOURCE_RES%~ STRING_EQUAL ~sw1h53~ ? 0x3f00ff00 :
							   ~%SOURCE_RES%~ STRING_EQUAL ~sw1h60~ ? 0x3f00ff00 :
							   ~%SOURCE_RES%~ STRING_EQUAL ~sw1h61~ ? 0x3f00ff00 : 0x00)
				FOR ("i1" = 0x00 ; "i1" < ("ge" * 0x30) ; "i1" += 0x30) BEGIN
					READ_SHORT ("eo" + "i1" + 0x00) "op"
					PATCH_IF (("op" = 0x08) OR ("op" = 0x09)) BEGIN
						READ_LONG ("eo" + "i1" + 0x08) "lc"
						PATCH_IF ("lc" = 0x10) BEGIN
							WRITE_LONG ("eo" + "i1" + 0x04) "blade"
							SET "blade" = 0x00
						END ELSE PATCH_IF ("lc" = 0x14) BEGIN
							WRITE_LONG ("eo" + "i1" + 0x04) "grip"
							SET "grip"  = 0x00
						END ELSE PATCH_IF ("lc" = 0x15) BEGIN
							WRITE_LONG ("eo" + "i1" + 0x04) "minor"
							SET "minor" = 0x00
						END
					END
				END
				PATCH_IF ("blade" != 0x00) BEGIN
					INSERT_BYTES ("eo" + (0x30 * "ge") + 0x00) 0x30
					WRITE_SHORT  ("eo" + (0x30 * "ge") + 0x00) 0x08
					WRITE_BYTE   ("eo" + (0x30 * "ge") + 0x02) 0x01
					WRITE_LONG   ("eo" + (0x30 * "ge") + 0x04) "blade"
					WRITE_LONG   ("eo" + (0x30 * "ge") + 0x08) 0x10
					WRITE_BYTE   ("eo" + (0x30 * "ge") + 0x0c) 0x02
					WRITE_BYTE   ("eo" + (0x30 * "ge") + 0x12) 0x64
					SET "ge" += 0x01
				END
				PATCH_IF ("grip" != 0x00) BEGIN
					INSERT_BYTES ("eo" + (0x30 * "ge") + 0x00) 0x30
					WRITE_SHORT  ("eo" + (0x30 * "ge") + 0x00) 0x08
					WRITE_BYTE   ("eo" + (0x30 * "ge") + 0x02) 0x01
					WRITE_LONG   ("eo" + (0x30 * "ge") + 0x04) "grip"
					WRITE_LONG   ("eo" + (0x30 * "ge") + 0x08) 0x14
					WRITE_BYTE   ("eo" + (0x30 * "ge") + 0x0c) 0x02
					WRITE_BYTE   ("eo" + (0x30 * "ge") + 0x12) 0x64
					SET "ge" += 0x01
				END
				PATCH_IF ("minor" != 0x00) BEGIN
					INSERT_BYTES ("eo" + (0x30 * "ge") + 0x00) 0x30
					WRITE_SHORT  ("eo" + (0x30 * "ge") + 0x00) 0x08
					WRITE_BYTE   ("eo" + (0x30 * "ge") + 0x02) 0x01
					WRITE_LONG   ("eo" + (0x30 * "ge") + 0x04) "minor"
					WRITE_LONG   ("eo" + (0x30 * "ge") + 0x08) 0x15
					WRITE_BYTE   ("eo" + (0x30 * "ge") + 0x0c) 0x02
					WRITE_BYTE   ("eo" + (0x30 * "ge") + 0x12) 0x64
					SET "ge" += 0x01
				END
				PATCH_IF (("blade" != 0x00) OR ("grip" != 0x00) OR ("minor" != 0x00)) BEGIN
					WRITE_SHORT 0x70 "ge"
					FOR ("i1" = 0x00; "i1" < (0x38 * "hc"); "i1" += 0x38) BEGIN
						WRITE_SHORT ("ho" + "i1" + 0x20) "ge"
						READ_SHORT  ("ho" + "i1" + 0x1e) "ec"
						SET "ge" += "ec"
					END
				END
			END
		BUT_ONLY
	END
END	// of ACTION_FOR_EACH ~item~

// Blackrazor +3
ACTION_IF (FILE_EXISTS_IN_GAME ~miscbc.itm~) BEGIN
	COPY_EXISTING ~miscbc.itm~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
			WRITE_ASCII 0x22 ~FS~	// Flaming sword - replaces S1 Long sword
		END
	BUT_ONLY
END


/* ==================== *
 *  FLAME SHORT SWORDS  *
 * ==================== */

PRINT @203003 // ~Installing flame short swords...~
SILENT

ACTION_IF (is_bg2 OR is_tob OR is_tutu OR is_iwd) BEGIN // (GAME_IS ~totlm how bg2 tob tutu tutu_totsc ca~)

	COPY ~1pp/additions/obj_1pp/wplf2inv.bam~ ~override~
	COPY ~1pp/additions/obj_1pp/wpmf2inv.bam~ ~override~
	COPY ~1pp/additions/obj_1pp/wpnf2inv.bam~ ~override~
	COPY ~1pp/additions/obj_1pp/wpsf2inv.bam~ ~override~
	COPY ~1pp/additions/obj_1pp/wplf2oin.bam~ ~override~
	COPY ~1pp/additions/obj_1pp/wpmf2oin.bam~ ~override~
	COPY ~1pp/additions/obj_1pp/wpnf2oin.bam~ ~override~
	COPY ~1pp/additions/obj_1pp/wpsf2oin.bam~ ~override~
	COPY ~1pp/additions/obj_1pp/wphf2oin.bam~ ~override~

END

COPY ~1pp/additions/obj_anim/f2/fs_s_files~ ~override~

// Chaos Blade
ACTION_IF (FILE_EXISTS_IN_GAME ~npsw06.itm~) BEGIN
	COPY_EXISTING ~npsw06.itm~ ~override~
		LPF ~1pp_npsw06~ END
	BUT_ONLY
END

// Arbane's Sword of Agility +2
ACTION_IF (FILE_EXISTS_IN_GAME ~sw1h27.itm~) BEGIN
	COPY_EXISTING ~sw1h27.itm~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
			WRITE_ASCII 0x22 ~F2~		// Flaming sword (blue) - replaces SS Short sword
			LPM ~clear~
			SET gradient = 25			// sets colour index 25 DARK_PURE_GOLD
			SET location = wgrey		// location (Head/Blade/Staff major)
			LPM ~colour~
			SET gradient = 89			// 89 METALLIC_PINK replaces colour index 33 CHROME_MAGENTA
			SET location = wred			// location (Grip/Staff minor - vanilla = whole staff)
			LPM ~colour~
			SET gradient = 33			// sets colour index 33 CHROME_MAGENTA
			SET location = wblue		// location (Head/Blade minor)
			LPM ~colour~
			SET setr = 0x34				// Set Red
			SET setg = 0x15				// Set Green
			SET setb = 0x0D				// Set Blue
			SET location = wblue		// location (Head/Blade minor)
			LPM ~glow~
		END
	BUT_ONLY
END

// Selûne's Blade
ACTION_IF (FILE_EXISTS_IN_GAME ~sblade.itm~) BEGIN
	COPY_EXISTING ~sblade.itm~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
			WRITE_ASCII 0x22 ~F2~		// Flaming sword (blue) - replaces FS Flaming sword
			LPM ~clear~
			SET gradient = 25			// sets colour index 25 DARK_PURE_GOLD
			SET location = wgrey		// location (Head/Blade/Staff major)
			LPM ~colour~
			SET gradient = 68			// sets colour index 68 SHINY_BLUE
			SET location = wred			// location (Grip/Staff minor - vanilla = whole staff)
			LPM ~colour~
			SET gradient = 71			// sets colour index 71 FROST_BLUE
			SET location = wblue		// location (Head/Blade minor)
			LPM ~colour~
			SET setr = 0x00				// Set Red
			SET setg = 0x00				// Set Green
			SET setb = 0x00				// Set Blue
			SET location = wgrey		// location (Head/Blade/Staff major)
			LPM ~glow~
			SET setr = 0x00				// Set Red
			SET setg = 0x00				// Set Green
			SET setb = 0x00				// Set Blue
			SET location = wred			// location (Grip/Staff minor - vanilla = whole staff)
			LPM ~glow~
			SET setr = 0x00				// Set Red
			SET setg = 0x00				// Set Green
			SET setb = 0x00				// Set Blue
			SET location = wblue		// location (Head/Blade minor)
			LPM ~glow~
		END
	BUT_ONLY
END

// Blade of Searing +3
ACTION_IF (FILE_EXISTS_IN_GAME ~sw1h39.itm~) BEGIN
	COPY_EXISTING ~sw1h39.itm~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
			WRITE_ASCII 0x22 ~FS~		// FS Flaming sword - replaces S1 Long sword
			LPM ~clear~
			SET gradient = 30			// sets colour index 30 LIGHT_IRON_GRAY
			SET location = wgrey		// location (Head/Blade/Staff major)
			LPM ~colour~
			SET gradient = 30			// sets colour index 30 LIGHT_IRON_GRAY
			SET location = wred			// location (Grip/Staff minor - vanilla = whole staff)
			LPM ~colour~
			SET gradient = 27			// sets colour index 27 GRAY
			SET location = wblue		// location (Head/Blade minor)
			LPM ~colour~
			SET setr = 0x19				// Set Red
			SET setg = 0x14				// Set Green
			SET setb = 0x0B				// Set Blue
			SET location = wred			// location (Grip/Staff minor - vanilla = whole staff)
			LPM ~glow~
		END
	BUT_ONLY
END

// Short Sword +1
ACTION_IF (FILE_EXISTS_IN_GAME ~sw1h98.itm~) BEGIN
	COPY_EXISTING ~sw1h98.itm~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
			WRITE_ASCII 0x22 ~SS~		// SS Short sword - replaces FS Flaming sword
		END
	BUT_ONLY
END


/* =================== *
 *  MODS FLAME SWORDS  *
 * =================== */

PRINT @203004 // ~Installing flame sword mod item patches (if found).~
SILENT

/* ----------- *
 *  Chloe mod  *
 * ----------- */

ACTION_IF (FILE_EXISTS_IN_GAME ~chlosw1.itm~) BEGIN

	COPY ~1pp/additions/obj_anim/f2/fs_s_items/cchak01.bam~ ~override~
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/cchak02.bam~ ~override~
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/cchak03.bam~ ~override~
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/cchla01.bam~ ~override~
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/cchla02.bam~ ~override~
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/cchla03.bam~ ~override~
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/ichak01.bam~ ~override~
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/ichak02.bam~ ~override~
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/ichak03.bam~ ~override~
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/ichla01.bam~ ~override~
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/ichla02.bam~ ~override~
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/ichla03.bam~ ~override~

	// Wrath of Akadia
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/chlosw1.itm~ ~override~
		SAY NAME1 @203101
		SAY NAME2 @203101
		SAY UNIDENTIFIED_DESC @203102
		SAY IDENTIFIED_DESC @203102
	BUT_ONLY

	// Rage of Akadia
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/chlosw2.itm~ ~override~
		SAY NAME1 @203103
		SAY NAME2 @203103
		SAY UNIDENTIFIED_DESC @203104
		SAY IDENTIFIED_DESC @203104
	BUT_ONLY

	// Fury of Akadia
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/chlosw3.itm~ ~Override~
		SAY NAME1 @203105
		SAY NAME2 @203105
		SAY UNIDENTIFIED_DESC @203106
		SAY IDENTIFIED_DESC @203106
	BUT_ONLY

	// Lathandar's Flame
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/chlosw4.itm~ ~override~
		SAY NAME1 @203107
		SAY NAME2 @203107
		SAY UNIDENTIFIED_DESC @203108
		SAY IDENTIFIED_DESC @203108
	BUT_ONLY

	// Lathandar's Blaze
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/chlosw5.itm~ ~override~
		SAY NAME1 @203109
		SAY NAME2 @203109
		SAY UNIDENTIFIED_DESC @203110
		SAY IDENTIFIED_DESC @203110
	BUT_ONLY

	// Lathandar's Inferno
	COPY ~1pp/additions/obj_anim/f2/fs_s_items/chlosw6.itm~ ~override~
		SAY NAME1 @203111
		SAY NAME2 @203111
		SAY UNIDENTIFIED_DESC @203112
		SAY IDENTIFIED_DESC @203112
	BUT_ONLY

END

/* -------------- *
 *  Yasraena mod  *
 * -------------- */

// Fireflower
ACTION_IF (FILE_EXISTS_IN_GAME ~fireflow.itm~) BEGIN
	COPY_EXISTING ~fireflow.itm~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
			WRITE_ASCII 0x22 ~F2~
			LPM ~clear~
			SET gradient = 12			// sets colour index 12 LIGHT_CARNATION_PINK
			SET location = wgrey		// location (Head/Blade/Staff major)
			LPM ~colour~
			SET gradient = 115			// sets colour index 115 SUNKISSED
			SET location = wred			// location (Grip/Staff minor - vanilla = whole staff)
			LPM ~colour~
			SET gradient = 70			// sets colour index 70 LIGHT_METALLIC_RED
			SET location = wblue		// location (Head/Blade minor)
			LPM ~colour~
			SET setr = 0x2B				// Set Red
			SET setg = 0x00				// Set Green
			SET setb = 0x00				// Set Blue
			SET location = wgrey		// location (Head/Blade/Staff major)
			LPM ~glow~
			SET setr = 0x2C				// Set Red
			SET setg = 0x05				// Set Green
			SET setb = 0x05				// Set Blue
			SET location = wblue		// location (Head/Blade minor)
			LPM ~glow~
		END
	BUT_ONLY
END

// Fireflower +5
ACTION_IF (FILE_EXISTS_IN_GAME ~fireflo2.itm~) BEGIN
	COPY_EXISTING ~fireflo2.itm~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
			WRITE_ASCII 0x22 ~F2~
			LPM ~clear~
			SET gradient = 96			// sets colour index 96 CHROME_BLUE
			SET location = wgrey		// location (Head/Blade/Staff major)
			LPM ~colour~
			SET gradient = 51			// sets colour index 51 DARK_OLIVE_GREEN
			SET location = wred			// location (Grip/Staff minor - vanilla = whole staff)
			LPM ~colour~
			SET gradient = 96			// sets colour index 96 CHROME_BLUE
			SET location = wblue		// location (Head/Blade minor)
			LPM ~colour~
			SET setr = 0x2C				// Set Red
			SET setg = 0x0C				// Set Green
			SET setb = 0x02				// Set Blue
			SET location = wgrey		// location (Head/Blade/Staff major)
			LPM ~glow~
			SET setr = 0x3C				// Set Red
			SET setg = 0x32				// Set Green
			SET setb = 0x18				// Set Blue
			SET location = wblue		// location (Head/Blade minor)
			LPM ~glow~
		END
	BUT_ONLY
END

/* ---------------- *
 *  IWD in BG2 mod  *
 * ---------------- */

// Flaming Long Sword +2
ACTION_IF (FILE_EXISTS_IN_GAME ~ulswd3a.itm~) BEGIN
	COPY_EXISTING ~ulswd3a.itm~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
			WRITE_ASCII 0x22 ~FS~
		END
	BUT_ONLY
END

/* ------------------------------- *
 *  Improved HaerDalis Swords mod  *
 * ------------------------------- */

// Chaos Blade
ACTION_IF (FILE_EXISTS_IN_GAME ~news06.itm~) BEGIN
	COPY_EXISTING ~news06.itm~ ~override~
		LPF ~1pp_npsw06~ END
	BUT_ONLY
END


PRINT @203005 // ~Restoring flame swords for BG1 animations...~
SILENT

COPY ~1pp/additions/obj_comp~ ~override~

ACTION_FOR_EACH category IN ~l~ ~m~ ~s~ BEGIN
	ACTION_FOR_EACH sequence IN ~a1~ ~a1e~ ~a3~ ~a3e~ ~a5~ ~a5e~ ~g1~ ~g1e~ ~w2~ ~w2e~ BEGIN
		COPY_EXISTING ~wp%category%fs%sequence%.bam~ ~override/wp%category%f2%sequence%.bam~
	END
END

// END of COMPONENT